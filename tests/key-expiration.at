# Copyright (C) 2017 Red Hat, Inc.  All rights reserved.
#
# This copyrighted material is made available to anyone wishing to use, modify,
# copy, or redistribute it subject to the terms and conditions of the GNU
# General Public License v.2.  This program is distributed in the hope that it
# will be useful, but WITHOUT ANY WARRANTY expressed or implied, including the
# implied warranties of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
# See the GNU General Public License for more details.  You should have
# received a copy of the GNU General Public License along with this program; if
# not, write to the Free Software Foundation, Inc., 51 Franklin Street, Fifth
# Floor, Boston, MA 02110-1301, USA.  Any Red Hat trademarks that are
# incorporated in the source code or documentation are not subject to the GNU
# General Public License and may only be used or replicated with the express
# permission of Red Hat, Inc.
#
# Red Hat Author: Patrick Uiterwijk <puiterwijk@redhat.com>

AT_SETUP([Key expiration modification check])


m4_include([include_setup.at])
m4_include([include_start.at])


# Import test key
AT_CHECK([printf 'rootroot\0imported\0imported-key-pw\0' | \
 		sigul -c client/client.conf --batch -v -v \
		import-key expired-key \
		"$abs_srcdir/tests/expired-key.asc"])
AT_CHECK([printf 'rootroot\0' | \
		sigul -c client/client.conf --batch -v -v \
		list-keys], ,
[expired-key
])
AT_CHECK([printf 'imported-key-pw\0' | \
		sigul -c client/client.conf --batch -v -v \
		get-public-key expired-key > public.asc])
AT_CHECK([gpg -q --homedir gnupg --import public.asc])
rm public.asc

# First make sure the key is expired
# Note: the importance of the chosen expiration date is left as an excersise to
# the reader.
AT_CHECK([GNUPGHOME=server/gnupg gpg --list-keys | grep "expired:"], 0,
         [pub   rsa1024 2017-02-26 [[SC]] [[expired: 2017-02-27]]
], [ignore])

# Set expiration for primary key
AT_CHECK([printf 'imported-key-pw\0' | \
        sigul -c client/client.conf --batch -v -v \
        change-key-expiration expired-key --expire-date 2030-01-01], 0, [], [])

AT_CHECK([GNUPGHOME=server/gnupg gpg --list-keys | grep expires], 0,
         [pub   rsa1024 2017-02-26 [[SC]] [[expires: 2030-01-01]]
], [ignore])

# Set expiration for subkey
AT_CHECK([printf 'imported-key-pw\0' | \
        sigul -c client/client.conf --batch -v -v \
        change-key-expiration expired-key --expire-date 2030-01-01 --subkey 1],
        0, [], [])

AT_CHECK([GNUPGHOME=server/gnupg gpg --list-keys | grep expires], 0,
         [pub   rsa1024 2017-02-26 [[SC]] [[expires: 2030-01-01]]
sub   rsa1024 2017-02-26 [[E]] [[expires: 2030-01-01]]
], [ignore])

# Clear expiration for primary key
AT_CHECK([printf 'imported-key-pw\0' | \
        sigul -c client/client.conf --batch -v -v \
        change-key-expiration expired-key],
        0, [], [])

AT_CHECK([GNUPGHOME=server/gnupg gpg --list-keys | grep expires], 0,
         [sub   rsa1024 2017-02-26 [[E]] [[expires: 2030-01-01]]
], [ignore])

# Clear expiration for subkey
AT_CHECK([printf 'imported-key-pw\0' | \
        sigul -c client/client.conf --batch -v -v \
        change-key-expiration expired-key --subkey 1],
        0, [], [])

AT_CHECK([GNUPGHOME=server/gnupg gpg --list-keys | grep expires], 1, [], [ignore])


m4_include([include_cleanup.at])
